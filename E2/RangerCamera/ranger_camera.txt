@name RangeFinder
@inputs Finder:wirelink Screen:wirelink Reset:number
@outputs XPixel:number YPixel:number
@persist FOV:number PER_X:number PER_Y:number WIDTH:number HEIGHT:number SLEEPING:number
@trigger 
@strict

MAX_DISTANCE = 500

TICK_RATE = 1
SLEEP_RATE = 1000

SUBTICKS = 64

function tickBody() {
    if (YPixel >= HEIGHT) {
        return 0
    }

    Finder[0] = 0
    Finder[6] = 1
    
    DeltaX = XPixel * PER_X
    DeltaY = YPixel * PER_Y
    
    Finder[2] = MAX_DISTANCE
    Finder[3] = 0
    
    Finder[4] = lerp(-FOV, FOV, DeltaX)
    Finder[5] = lerp(-FOV, FOV, DeltaY)
    
    Finder[0] = 1
    Dist = Finder[1]
    Dist /= MAX_DISTANCE
    
    Coord = (XPixel + (YPixel * WIDTH)) * 3
    Frag = Dist
   
    Screen[1048569] = 1
    
    Screen[Coord] = Frag * 255
    Screen[Coord + 1] = Frag * 255
    Screen[Coord + 2] = Frag * 255

    XPixel++
    
    if (XPixel >= WIDTH) {
        YPixel++
        XPixel = 0
    }
    
    return 1
}

if (first()) {
    SLEEPING = 0
    
    WIDTH = 64
    HEIGHT = 64
    
    XPixel = 0
    YPixel = 0
    
    FOV = 0.5
    
    PER_X = (1.0 / (WIDTH - 1))
    PER_Y = (1.0 / (HEIGHT - 1))
    
    interval(TICK_RATE)
}

if (clk()) {
    if (Reset == 1) {
        reset()
        return
    }
    
    Failed = 0

    if (SLEEPING == 0) {   
        for (I = 0, SUBTICKS) {
            if (tickBody() == 0) {
                Failed = 1
            }
        }
    }
    
    if (Failed == 0) {
        interval(TICK_RATE)
    } else {
        SLEEPING = 1
        interval(SLEEP_RATE)
    }
}
